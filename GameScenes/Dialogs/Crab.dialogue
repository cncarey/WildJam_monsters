~ Start
do pauseTimer()
set Global.isTalking = true
if Global.quests.Crab.QuestStarted == false
	=> Pre_Quest
else
	=> Update_Quest

~ Pre_Quest
Crab: ...
{{Global.playerName}} 
- Why are you terrorizing to town?
	Crab: What are you talking about. What town?
	Crab: I've been here in the swell all week.
	=> Start_Quest
- Are you okay?
	Crab: No
	set Global.quests.Morality += 1
	=> Start_Quest
- End the conversation 
	set Global.isTalking = false
	do restartTimer()
	=> END

~ Start_Quest
Crab: I was surfing a HUGE wave...maybe a bit too huge
Crab: ...When I lost controll and landed up here.
Crab: I dropped all my rubies and I need help finidng them
{{Global.playerName}}: I can help you with that.
Crab: Really. I'm missing {{Global.quests.Crab.totalItems}} rubies throught the map.
{{Global.playerName}}: So if I find your {{Global.quests.Crab.totalItems}} rubies you'll head back to the sea
Crab: Yeah. And can you please put them at the big flag after you find them?
set Global.quests.Crab.QuestStarted = true
do startQuest(Global.quests.Crab.QuestItem, "Crab")
set Global.isTalking = false
do restartTimer()
=> END

~ Update_Quest
if  Global.quests.Crab.CurrQuestItemCount >= Global.quests.Crab.totalItems 
	Crab: You got all my rubies. Let's find the flag!
elif Global.quests.Crab.CurrQuestItemCount >= Global.quests.Crab.totalItems/2
	Crab: Yeah we don't have to many more to find
else
	Crab: Let's find my rubies.
set Global.isTalking = false
do restartTimer()
=> END!
